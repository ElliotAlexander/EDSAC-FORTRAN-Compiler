      SUBROUTINE ENERGY(FLAST,ISENSE)
      COMMON/CB01/DCSL(10),BETA(10),SNA(10),C0(10),X0ALO(10),
     XX0SIO(10),D(10),VB(10),CM0(10),SND(10)
      COMMON/CB04/ACC1,ACC2,IFLAG
      COMMON/CB05/NSET,ISET,NSTEP,ISTEP
      COMMON/CB17/TOTUAQ,TOTUSL,TOTUSI,UMIN,UTOT
C      SET UMIN TO INCREDIBLY HIGH VALUE SO THAT ALL POSSIBLE SYSTEM
C      ENERGIES WILL BE LESS THAN IT
      DATA UMIN/1.0E74/
      DATA UX/1.0E60/
      DATA FF/1000.0/
C      SUBROUTINE TO CALCULATE THE OVERALL ELECTROSTATIC ENERGY
C      OF THE SYSTEM, AND STORE THE CONTENTS OF THE BOUNDARY CONDITION
C      COMMON BLOCKS (CB07 AND CB08) IF ITS THE LOWEST.
C      ISENSE IS A FLAG TO BE SET IF A SENSIBLE SOLUTION IS FOUND
C      FLAST CONTAINS THE VALUE OF THE CLOSED LOOP VOLTAGE
C      IF > FF% OF VB, DON'T BOTHER WITH ENERGY, AS
C      LOOP HAS FAILED TO CLOSE
      IF(ABS(FLAST).GT.(FF*VB(ISET)))UTOT=UX
      IF(ABS(FLAST).GT.(FF*VB(ISET)))RETURN
C      AQUEOUS PHASE
      CALL AQENER
C      SURFACE LAYER
C      (USE ONE OF TWO ROUTINES, DEPENDING ON DUMMY FIELD FLAG)
      IF(IFLAG.EQ.0)CALL SLEN0
      IF(IFLAG.NE.0)CALL SLEN1
C      SILICON
      CALL SIENER
C      CALCULATE TOTAL ENERGY
      UTOT=TOTUAQ+TOTUSL+TOTUSI
      CALL CK(66,TOTUAQ,'TOTUAQ','ENERGY')
      CALL CK(67,TOTUSL,'TOTUSL','ENERGY')
      CALL SELECT
      CALL CK(68,TOTUSI,'TOTUSI','ENERGY')
      CALL CK(69,UTOT  ,'UTOT  ','ENERGY')
C      SAVE THE SMALLEST
      UMIN=AMIN1(UMIN,UTOT)
      CALL CK(70,UMIN  ,'UMIN  ','ENERGY')
      IF(UMIN.EQ.UTOT)CALL STORE
C      IF ENERGY SENSIBLE, SET ISENSE
      IF(UMIN.LT.1.0E10)ISENSE=1
      RETURN
      END
C
C******
C
C
      SUBROUTINE RELOAD
      COMMON/CB04/ACC1,ACC2,IFLAG
      COMMON/CB05/NSET,ISET,NSTEP,ISTEP
      COMMON/CB07/V(7)
      COMMON/CB08/E(8)
      COMMON/CB16/VSTORE(7),ESTORE(8),IBFLAG
      COMMON/CB24/PSILAR(12),VAAR(12)
C      ROUTINE TO RELOAD THE CONTENTS
C      OF THE TEMPORARY BOUNDARY CONDITION STORE (CB16)
C      BACK INTO THE BOUNDARY CONDITION COMMON BLOCKS.
C      BY NOW CB16 SHOULD HOLD THE BOUNDARY CONDITIONS FOR THE
C      CONFIGURATION WITH THE LOWEST ELECTROSTATIC ENERGY.
C      WATCH OUT FOR SNEAKY TRICKS WITH COMMON.
C      BOUNDARY VOLTAGES
      DO 100 I=1,7
      V(I)=VSTORE(I)
100   CONTINUE
C      BOUNDARY FIELDS
      DO 200 I=1,8
      E(I)=ESTORE(I)
200   CONTINUE
C      RESTORE DUMMY FIELD FLAG
      IFLAG=IBFLAG
C      AND SAVE THE TWO INTERFACE VOLTAGES
C      AQUEOUS INTERFACE VOLTAGE, (VA):
      VAAR(ISET)=VSTORE(4)
C      VOLTAGE DROP  ACROSS SURFACE LAYER, (PSIL):
      PSILAR(ISET)=VSTORE(3)
      RETURN
      END
C
C******
C
C
      SUBROUTINE STORE
C      WATCH OUT FOR THE SNEAKY TRICKS WITH COMMON
      COMMON/CB04/ACC1,ACC2,IFLAG
      COMMON/CB07/V(7)
      COMMON/CB08/E(8)
      COMMON/CB16/VSTORE(7),ESTORE(8),IBFLAG
C      ROUTINE TO STORE THE CONTENTS OF THE BOUNDARY CONDITION
C      COMMON BLOCKS (CB07 AND CB08)
C      IN BLOCK CB16
C      BOUNDARY VOLTAGES
      DO 100 I=1,7
      VSTORE(I)=V(I)
100   CONTINUE
C      BOUNDARY FIELDS
      DO 200 I=1,8
      ESTORE(I)=E(I)
200   CONTINUE
C      STORE THE DUMMY FIELD FLAG
      IBFLAG=IFLAG
      RETURN
      END
C
C******
C
C
