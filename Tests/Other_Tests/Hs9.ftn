      SUBROUTINE SLPROF
      COMMON/CB01/DCSL(10),BETA(10),SNA(10),C0(10),X0ALO(10),
     XX0SIO(10),D(10),VB(10),CM0(10),SND(10)
      COMMON/CB02/Q,DC0,DCSI,DCALO,DCSIO,CCI,DCAQ0
      COMMON/CB04/ACC1,ACC2,IFLAG
      COMMON/CB05/NSET,ISET,NSTEP,ISTEP
      COMMON/CB06/C1,C2,C3,C4,C5,C6,C7,VINV,EINV,VSTART,VEND
      COMMON/CB07/PHIS,VI,PSIL,VA,VALOX,VGATE,VSIOX
      COMMON/CB08/EA,EPD,B,EXPBV,EP0,ES,EALOX,ESIOX
      COMMON/CB11/VSL(100,10),ESL(100,10),QSL(100,10),XSL(100),LIMSL,
     XXSLLOC,XMAXSL
      COMMON/CB13/C8,C9,C10,C11,C12,C13,C14,C15,C16,C17,C18,C19,C20,C21
C      ROUTINE TO CALCULATE CHARGE DENSITY, ELECTRIC FIELD AND
C      VOLTAGE PROFILES IN THE SURFACE LAYER, GIVEN THE BOUNDARY
C      CONDITIONS ESTABLISHED BY SUBROUTINE CLOSLP
C      CALCULATE X INCREMENT
      XINC=D(ISET)/FLOAT(LIMSL)
C      CALCULATE HORRIBLE CONSTANT
      HCE=ADBEXP(-BETA(ISET)*VA)
      CL1=C4*HCE
C      CALCULATE SECOND HORRIBLE CONSTANT
      CL2=C13*HCE
C      SE; UP LOOP TO ESTABLISH VOLTAGE, FIELD AND CHARGE DENSITY
C      PROFILES
      DO 100 I=1,LIMSL
C      POSITION IN LAYER
      XSLLOC=FLOAT(I-1)*XINC
C      GET VOLTAGE (NB VALUE HELD IN IFLAG IS STILL VALID)
      IF(IFLAG.EQ.0)CALL GPSIL0
      IF(IFLAG.NE.0)CALL GPSIL1
C      AND STORE RESULT
      VSL(I,ISET)=PSIL
C      IF FIRST TIME ROUND, STORE POSITION COORDINATE
      IF(ISET.EQ.1)XSL(I)=XSLLOC
C      EXPONENT OF NORMALISED VOLTAGE
      TEXP=ADBEXP(-BETA(ISET)*PSIL)
C      CHARGE DENSITY
      QSL(I,ISET)=CL2*TEXP
C      FIELD
      TESL=(EPD*EPD)-(CL1*(1.0-TEXP))
      IF(TESL.LT.0.0)CALL PANIC(10)
C      CALCULATE SIGN OF FIELD.
C      SIGN IS CALCULATED FROM DERIVATIVE INFORMATION, SO WE
C      NEED THE POSITION COORDINATE
      IF(IFLAG.EQ.0)TESL=ESURF0(XSLLOC)
      IF(IFLAG.NE.0)TESL=ESURF1(XSLLOC)
      ESL(I,ISET)=TESL
100   CONTINUE
      RETURN
      END
C
C******
C
C
      FUNCTION ESURF0(X)
      COMMON/CB01/DCSL(10),BETA(10),SNA(10),C0(10),X0ALO(10),
     XX0SIO(10),D(10),VB(10),CM0(10),SND(10)
      COMMON/CB05/NSET,ISET,NSTEP,ISTEP
      COMMON/CB08/EA,EPD,B,EXPBV,EP0,ES,EALOX,ESIOX
C      FUNCTION TO RETURN THE ELECTRIC FIELD IN THE SURFACE
C      LAYER AT POSITION X, GIVEN IFLAG = 0
      S1=EPD/B
      S0=B*BETA(ISET)*(X-D(ISET))/2.0
      TS0=TAN(S0)
      ESURF0=B*((S1+TS0)/(1.0-(S1*TS0)))
      RETURN
      END
C
C******
C
C
      FUNCTION ESURF1(X)
      COMMON/CB01/DCSL(10),BETA(10),SNA(10),C0(10),X0ALO(10),
     XX0SIO(10),D(10),VB(10),CM0(10),SND(10)
      COMMON/CB05/NSET,ISET,NSTEP,ISTEP
      COMMON/CB08/EA,EPD,B,EXPBV,EP0,ES,EALOX,ESIOX
C      FUNCTION TO RETURN THE ELCTRIC FIELD IN THE SURFACE LAYER,
C      GIVEN IFLAG = 1
      S1=EPD/B
      S0=B*BETA(ISET)*(X-D(ISET))/2.0
      S1P=S1+1.0
      S1M=S1-1.0
      E=ADBEXP(2.0*S0)
      ESURF1=B*(S1P+(S1M*E))/(S1P-(S1M*E))
      RETURN
      END
C
C******
C
C
      SUBROUTINE SIPROF
      COMMON/CB01/DCSL(10),BETA(10),SNA(10),C0(10),X0ALO(10),
     XX0SIO(10),D(10),VB(10),CM0(10),SND(10)
      COMMON/CB02/Q,DC0,DCSI,DCALO,DCSIO,CCI,DCAQ0
      COMMON/CB05/NSET,ISET,NSTEP,ISTEP
      COMMON/CB06/C1,C2,C3,C4,C5,C6,C7,VINV,EINV,VSTART,VEND
      COMMON/CB07/PHIS,VI,PSIL,VA,VALOX,VGATE,VSIOX
      COMMON/CB08/EA,EPD,B,EXPBV,EP0,ES,EALOX,ESIOX
      COMMON/CB12/VSI(100,10),ESI(100,10),QSI(100,10),XSI(100),LIMSI,
     XXSILOC,XMAXSI
      COMMON/CB13/C8,C9,C10,C11,C12,C13,C14,C15,C16,C17,C18,C19,C20,C21
C      ROUTINE TO CALCULATE CHARGE DENSITY, ELECTRIC FIELD AND
C      VOLTAGE PROFILES IN THE SILICON, USING THE DEPLETION
C      APPROXIMATION, GIVEN THE SURFACE FIELD AND POTENTIAL
C      AS SUPPLIED BY SUBROUTINE CLOSLP
C      CALCULATE DEPTH OF DEPLETION REGION
C      DEPTH USED IS THAT OF MAXIMUM DEPLETION WIDTH THAT WILL
C      OCCOUR IN THE COMPLETE SET OF PROFILES.
C      CALCULATED BY S/Y SETMTH, AS ITS A FUNCTION OF JISET
      XDEP=C15*SQRT(PHIS)
C      CALCULATE SURFACE FIELD
      ESURF=ES
      IF(PHIS.GT.VINV)ESURF=EINV
C      CALCULATE X INCREMENT
      XINC=XDEP/FLOAT(LIMSI)
C      SET UP LOOP TO CALCULATE PROFILES AND STORE THEM
      DO 100 I=1,LIMSI
      FF=(1.0-(X/XDEP))
C      X-COORDINATE
      X=FLOAT(I-1)*XINC
C      VOLTAGE
      VSI(I,ISET)=PHIS*FF*FF
C      FIELD
      ESI(I,ISET)=ESURF*FF
C      CHARGE
      QSI(I,ISET)=SNA(ISET)
C      POSITION
C      (IF FIRST TIME ROUND)
      IF(ISET.EQ.1)XSI(I)=X
100   CONTINUE
C      SO ALL PROFILES NICELY TUCKED UP IN DATA ARRAYS
C      IF SURFACE INVERTED, LOAD INVERSION CHARGE INTO
C      FIRST ELEMENT OF CHARGE ARRAY
      IF(PHIS.GT.VINV)QSI(1,ISET)=(ES*DCSIO)-(EINV*DCSI)
      RETURN
      END
C
C******
C
C
      SUBROUTINE CKPLOT
      COMMON/CB05/NSET,ISET,NSTEP,ISTEP
      COMMON/CB06/C1,C2,C3,C4,C5,C6,C7,VINV,EINV,VSTART,VEND
      COMMON/CB11/VSL(100,10),ESL(100,10),QSL(100,10),XSL(100),LIMSL,
     XXSLLOC,XMAXSL
      COMMON/CB12/VSI(100,10),ESI(100,10),QSI(100,10),XSI(100),LIMSI,
     XXSILOC,XMAXSI
      COMMON/CB15/IPTR,POSSVA(2,100),LPOSVA
C      THIS ROUTINE CALCULATES THE FUNCTION F AS A FUNCTION OF
C      VA, AND WRITES THE VALUES TO A FILE ON CHANNEL 2, SO THAT IF THE
C      PROGRAM CRASHES LATER, THE FILE CAN BE PICKED UP BY A
C      SUBSEQUENT JOB, AND PLOTTED
C      NUMBER OF POINTS
      NPTS=NSTEP
      ANPTS=FLOAT(NPTS-1)
      CX=(VSTART-VEND)/ANPTS
C      F IS PLOTTED BETWEEN V = VSTART AND V = VEND
C      INITIALISE F
      XSILOC=0.0
      XSLLOC=0.0
C      GET THE SIGN OF THE FIRST VALUW OF F
      SFOLD=S(F(VSTART))
C      AND THE FIRST VOLTAGE VALUE
      VOLD=VSTART
C      SET UP LOOP
      DO 100 I=1,NPTS
      FI=FLOAT(I)
      CALL CK(60,FI,'I     ','CKPLOT')
C      CALCULATE VOLTAGE VALUE
      V=(FLOAT(NPTS-I)*CX)+VEND
C      CALCULATE F-VALUE
      FV=F(V)
C      STORE THE SIGN
      SFV=S(FV)
C      IF THIS IS DIFFERENT FROM THE PREVIOUS SIGN, WE'VE CROSSED
C      A SOLUTION, SI STORE STRADDLING VOLTAGE VALUES
C      AND UPDATE POINTER
      IF(SFOLD.NE.SFV)CALL UPDATE(V,VOLD)
C      STORE CURRENT VOLTAGE VALUE AND FUNCTION SIGN FOR NEXT TIME
      SFOLD=SFV
      VOLD=V
C      AND STORE
      WRITE(2,2000)V,FV
2000  FORMAT(2(1X,1PE15.8))
100   CONTINUE
C      OK, SO PLOT WRITTEN
C      AND ALL POSSIBLE SOLUTIONS PINNED DOWN.
C      START POINTS FORCBISECTION SOLUTION TUCKED UP IN POSSVA.
      RETURN
      END
C
C******
C
C
      SUBROUTINE SLEN1
      COMMON/CB01/DCSL(10),BETA(10),SNA(10),C0(10),X0ALO(10),
     XX0SIO(10),D(10),VB(10),CM0(10),SND(10)
      COMMON/CB02/Q,DC0,DCSI,DCALO,DCSIO,CCI,DCAQ0
      COMMON/CB05/NSET,ISET,NSTEP,ISTEP
      COMMON/CB08/EA,EPD,B,EXPBV,EP0,ES,EALOX,ESIOX
      COMMON/CB13/C8,C9,C10,C11,C12,C13,C14,C15,C16,C17,C18,C19,C20,C21
      COMMON/CB17/TOTUAQ,TOTUSL,TOTUSI,UMIN,UTOT
C      ROUTINE TO CALCULATE THE TOTAL ELECTROSTATIC ENERGY
C      IN THE SURFACE LAYER AS A FUNCTION OF THE BOUNDARY VALUES
C      OF THE SYSTEM. THIS EXPRESSION IS ONE OF TWO USED,
C      DEPENDING ON THE VALUE OF THE DUMMY FIELD FLAG, IFLAG.
C      THIS ROUTINE CALLED IF IFLAG = 1
      U1=1.0/(2.0*B)
      E1=EXP(-B*BETA(ISET)*D(ISET))
      SP=EPD+B
      SM=EPD-B
      U=U1*((E1-1.0)/((E1*SM)-SP))
      TOTUSL=DC0*DCSL(ISET)*((B*B*D(ISET))+U)
      IF(TOTUSL.LT.0.0)CALL PANIC(-2)
      RETURN
      END
C
C******
C
C
      SUBROUTINE SLEN0
      COMMON/CB01/DCSL(10),BETA(10),SNA(10),C0(10),X0ALO(10),
     XX0SIO(10),D(10),VB(10),CM0(10),SND(10)
      COMMON/CB02/Q,DC0,DCSI,DCALO,DCSIO,CCI,DCAQ0
      COMMON/CB05/NSET,ISET,NSTEP,ISTEP
      COMMON/CB08/EA,EPD,B,EXPBV,EP0,ES,EALOX,ESIOX
      COMMON/CB13/C8,C9,C10,C11,C12,C13,C14,C15,C16,C17,C18,C19,C20,C21
      COMMON/CB17/TOTUAQ,TOTUSL,TOTUSI,UMIN,UTOT
C      ROUTINE TO CALCULATE THE TOTAL ELECTROSTATIC ENERGY
C      IN THE SURFACE LAYER AS A FUNCTION OF THE BOUNDARY VALUES
C      OF THE SYSTEM. THIS EXPRESSION IS ONE OF TWO USED,
C      DEPENDING ON THE VALUE OF THE DUMMY FIELD FLAG, IFLAG.
C      THIS ROUTINE CALLED IF IFLAG = 0
      OFFSET=1.0E30
      S0X=B*BETA(ISET)*D(ISET)/2.0
      CALL CK(71,S0X,'S0X   ','SLEN0 ')
      S1=EPD/B
      CALL CK(72,S1,'S1    ','SLEN0 ')
      T=TAN(S0X)
      CALL CK(73,T,'T     ','SLEN0 ')
      U=(C12/(2.0*B))*(T/(1.0+(S1*T)))
      CALL CK(74,U,'U     ','SLEN0 ')
C      IF INTEGRATION INTERVAL STRADDLES A SINGULARITY, KEEP OFFSET
C      TO ARBITRARILY HIGH VALUE TO ENSURE REJECTION OF SOLUTION
C      CALCULATE POSITION OF SINGULARITIES STRADDLING THE
C      UPPER INTEGRATION BOUND
C      UPPER BOUND :
      BU=ATAN(S1)
C      LOWER BOUND :
      BL=BU-S0X
C      GET SINGULARITIES THAT STRADDLE
C      UPPER BOUND:
      CALL SINGUL(BU,SL,SU)
      CALL CK(75,SL,'SL    ','SLEN0 ')
      CALL CK(76,SU,'SU    ','SLEN0 ')
C      SO IF LOWER BOUND BETWEEN UPPER AND LOWER SINGULARITIES,
C      ALL IS SERENE, SO KILL OFFSET
      IF((SL.LT.BL).AND.(SU.GT.BL))OFFSET=0.0
      TOTUSL=DC0*DCSL(ISET)*((-B*B*D(ISET))+U)+OFFSET
C      IF NOT, SCREAM
      IF(TOTUSL.LT.0.0)CALL PANIC(-3)
      RETURN
      END
C
C******
C
C
      SUBROUTINE SINGUL(X,SL,SU)
C      ROUTINE TO RETURN THE SINGULRITIES OF SEC^2(X)
C      STRADDLING X.
C      UPPER SINGULARITY = SU
C      LOWER SINGULARITY = SL
C      ALGORITHM IS NOT TRIVIAL - SEE  NOTES
      DATA PI/3.141593/,PI2/1.5707965/
      S1=PI2*AINT(X/PI2)
      IF(AMOD(1.0001*ABS(S1),PI).LT.0.3)S1=S1-SIGN(PI2,X)
      S2=S1+SIGN(PI,X)
      SU=S2
      SL=S1
      IF(X.LT.0.0)SU=S1
      IF(X.LT.0.0)SL=S2
      RETURN
      END
C
C******
C
C
      SUBROUTINE SIENER
      COMMON/CB02/Q,DC0,DCSI,DCALO,DCSIO,CCI,DCAQ0
      COMMON/CB08/EA,EPD,B,EXPBV,EP0,ES,EALOX,ESIOX
      COMMON/CB17/TOTUAQ,TOTUSL,TOTUSI,UMIN,UTOT
C      ROUTINE TO CALCULATE THE TOTAL ELECTROSTATIC ENERGY
C      STORED IN THE CONVENTIONBAL TRANSISTOR STRUCTURE
C      ALUMINIUM OXIDE :
      UALOX=DC0*DCALO*EALOX*EALOX
C      SILICON DIOXIDE :
      USIOX=DC0*DCSIO*ESIOX*ESIOX
C      TOTAL ENERGY
      TOTUSI=UALOX+USIOX
      RETURN
      END
C
C******
C
C
      SUBROUTINE SELECT
      COMMON/CB15/IPTR,POSSVA(2,100),LPOSVA
      COMMON/CB17/TOTUAQ,TOTUSL,TOTUSI,UMIN,UTOT
      RETURN
      END
C
C******
C
C
