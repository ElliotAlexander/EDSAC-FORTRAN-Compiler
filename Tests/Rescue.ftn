      PROGRAM KLUTZ
      COMMON/CB101/NPTS,IFAIL,DNULL
C      THIS PROGRAM PICKS UP THE DATA FOR THE PLOTS
C      OF F VS V WRITTEN TO FILE CKDAT BY THE PREVIOUS PROGRAM. CKDAT
C      IS STILL ASSIGNED TO CHANNEL 2, AND THE PLOT IS WRITTEN
C      TO CHANNEL 9. DIAGNOSTIC MESSAGES FROM THIS PROGRAM ARE
C      OUTPUT ON CHANNEL 3. EACH GRAPH CONSISTS OF 250 PAIRS OF DATA
C      POINTS, AND THE FILE MAY CONTAIN UP TO 10 GRAPHS. THE FILE CAN
C      BE TRUNCATED AT ANY POINT DUE TO EXECUTION FAILURE OF
C      THE PREVIOUS PROGRAM, SO THIS PROGRAM HAS TO BE TOLERANT OF THAT.
C      AFTER HAVING DRAWN ALL THE GRAPHS SEPERATELY, THE PROGRAM
C      REWINDS THE DATA FILE AND SUPERPOSES ALL THE DATA ON THE
C      SAME SET OF AXES.
C      SET UP PROGRAM AND OPEN GRAPHICS
      CALL SETUP
C      SET UP LOOP TO DEAL WITH EACH GRAPH
      DO 100 I=1,10
C      UNLOAD DATA FROM FILE
      CALL UNLOAD(I)
C      SO PLOT IT (NB PLOTIT IS TOLERANT OF INCOMPLETE DATA)
      CALL PLOTIT(I)
C      IF IFAIL IS SET, SUBROUTINE UNLOAD HAS ENCOUNTERED
C      AN EOF MARKER, (OR TRANSFER ERROR) SO CLOSE PLOTTING
C      ROUTINES
      IF(IFAIL.NE.0)CALL CLOSPL
C      NB NO RETURN FROM CLOSPL
100   CONTINUE
C      ALL GRAPHS COMPLETE, CLOSE PLOT ROUTINES
      CALL CLOSPL
      END
C
C******
C
C
      SUBROUTINE SETUP
      COMMON/CB101/NPTS,IFAIL,DNULL
      COMMON/CB102/XDAT(2502),YDAT(2502)
      COMMON/CB103/XLEN,YLEN,YFIRST,YDELTA,XFIRST,XDELTA,DASH,Y0,XLAST
C      ROUTINE TO INITIALISE PROGRAM
C      OPEN GRAPHICS ON CHANNEL 9, AND MOVE PEN TO A POSITION TO
C      START PLOT
      CALL PLOTS(0,0,9)
C      MOVE PEN TO INITIAL POSITION, AND ESTABLISH NEW ORIGIN
      CALL PLOT(5.0,5.0,-3)
C      EOF FLAG
      IFAIL=0
C      LENGTH OF X-AXIS ON GRAPHS
      XLEN=30.0
C      LENGTH OF Y-AXIS ON GRAPHS
      YLEN=22.0
C      NULL DATA VALUE
      DNULL=-10.0
C      FIRST VALUE ON Y-AXIS
      YFIRST=DNULL*1.1
C      Y INCREMENT
      YDELTA=2.0*ABS(YFIRST)/YLEN
C      FIRST AND LAST VALUES ON X - AXIS
C      AND NUMBER OF DATA POINTS PER CHECKPLOT
C      THESE ARE PASSED THROUGH FROM FIRST PROGRAM ON CHANNEL 2
      READ(2,2000)XFIRST,XLAST,NPTS
2000  FORMAT(2(1X,1PE15.8),1X,I6)
C      X INCREMENT
      XDELTA=(XLAST-XFIRST)/XLEN
C      DASHED LINE
      DASH=0.5
C      Y COORDINATE OF ZERO LINE
      Y0=-YFIRST/YDELTA
C      LOAD SCALING PARAMETERS INTO DATA ARRAYS
      NPTS1=NPTS+1
      NPTS2=NPTS+2
      XDAT(NPTS1)=XFIRST
      XDAT(NPTS2)=XDELTA
      YDAT(NPTS1)=YFIRST
      YDAT(NPTS2)=YDELTA
      RETURN
      END
C
C******
C
C
      SUBROUTINE UNLOAD(NGRA)
      COMMON/CB101/NPTS,IFAIL,DNULL
      COMMON/CB102/XDAT(2502),YDAT(2502)
      COMMON/CB103/XLEN,YLEN,YFIRST,YDELTA,XFIRST,XDELTA,DASH,Y0,XLAST
C      ROUTINE TO READ THE DATA POINTS FROM THE FILE OPEN
C      ON CHANNEL 2. IF AN EOF MARKER IS DETECTED, THE FLAG IFAIL
C      IS SET, AND THE REMAINDER OF THE DATA ARRAY IS FILLED WITH
C      NULL VALUES
C      SET UP LOOPS TO READ DATA ONE RECORD AT A TIME
      DO 100 I=1,NPTS
C      SAVE CONTROL PARAMETER
      II=I
      READ(2,2000,END=1,ERR=1)XDAT(I),YDAT(I)
2000  FORMAT(2(1X,1PE15.8))
C      CLIP DATA IF NECESSARY TO MAKE CHECKPOINT PLOT
C      EASILY READABLE
      IF(YDAT(I).GT.ABS(DNULL))YDAT(I)=ABS(DNULL)
      IF(YDAT(I).LT.DNULL)YDAT(I)=DNULL
100   CONTINUE
C      LEGAL EXIT FROM LOOP - ALL DATA PRESENT
      RETURN
1     CONTINUE
C      OK, SO DATA LIST INCOMPLETE
C      SET FAILURE FLAG
      IFAIL=1
C      FILL REMAINING ELEMENTS OF DATA ARRAYS WITH NULL VALUES
      DO 200 I=II,NPTS
      XDAT(I)=XLAST
      YDAT(I)=DNULL
200   CONTINUE
      RETURN
      END
C
C******
C
C
      SUBROUTINE PLOTIT(I)
      COMMON/CB101/NPTS,IFAIL,DNULL
      COMMON/CB102/XDAT(2502),YDAT(2502)
      COMMON/CB103/XLEN,YLEN,YFIRST,YDELTA,XFIRST,XDELTA,DASH,Y0,XLAST
C      ROUTINE TO PLOT THE DATA CONTAINED IN THE ARRAYS XDAT, YDAT
C      AS THE Y DATA IS CONSTRAINED TO +/- DNULL, AND
C      THE X DATA IS CONSTRAINED TO THE SAME SET EVERY TIME,
C      SCALING EACH TIME IS UNNECCESARY. ALL THE REQUIRED INCREMENTS
C      AND START VALUES ARE CALCULATED IN SUBROUTINE SETUP, AND PASSED
C      THROUGH COMMON BLOCK /CB103/
      DIMENSION ITIT(7)
      DATA IXTIT/4HVA  /,IYTIT/4HF   /
      DATA ITIT(1)/4HF VS/,ITIT(2)/4H. V /,ITIT(3)/4HFOR /
      DATA ITIT(4)/4HPARA/,ITIT(5)/4HMETE/,ITIT(6)/4HR SE/
      DATA ITIT(7)/4HT   /
C      PEN IS ALREADY IN POSITION TO START, SO DRAW AXES
C      LOWER X-AXIS
      CALL AXIS(0.0,0.0,IXTIT,-2,XLEN,0.0,XFIRST,XDELTA)
C      LEFT Y-AXIS
      CALL AXIS(0.0,0.0,IYTIT,+1,YLEN,90.0,YFIRST,YDELTA)
C      UPPER X-AXIS
      CALL AXIS(0.0,YLEN,IXTIT,+2,XLEN,0.0,XFIRST,XDELTA)
C      RIGHT Y-AXIS
      CALL AXIS(XLEN,0.0,IYTIT,-1,YLEN,90.0,YFIRST,YDELTA)
C      MOVE PEN TO POSITION TO DRAW X = 0.0 LINE
      CALL PLOT(0.0,Y0,3)
C      DRAW X = 0.0 LINE
      CALL DASHP(XLEN,Y0,DASH)
C      PLOT CURVE
      CALL LINE(XDAT,YDAT,NPTS,1,0,0)
C      WRITE GRAPH TITLE
      FI=FLOAT(I)
C      NOTE VAX F77 SYMBOL CALL
      CALL SYMBOL(3.0,-3.0,0.5,ITIT,0,0.0,27)
      CALL NUMBER(999.0,999.0,0.5,FI,0.0,-1)
C      MOVE PEN TO POSITION TO START NEXT GRAPH,
C      AND ESTABLISH A NEW ORIGIN
      CALL PLOT(XLEN+5.0,0.0,-3)
      RETURN
      END
C
C******
C
C
      SUBROUTINE CLOSPL
      COMMON/CB101/NPTS,IFAIL,DNULL
      COMMON/CB102/XDAT(2502),YDAT(2502)
      COMMON/CB103/XLEN,YLEN,YFIRST,YDELTA,XFIRST,XDELTA,DASH,Y0,XLAST
C      ROUTINE TO CLOSE PLOTTING PACKAGE IN THE EVENT
C      OF THE DATA FILE BEING INCOMPLETE
C      NB ALSO CALLED ON LEGITIMATE TERMINATION
C      DATA FILE IS REWOUND, AND DATA REPLOTTED,
C      ALL SUPERPOSED ON THE SAME AXES
      REWIND 2
C      3HROW AWAY FIRST RECORD OF DATA FILE, AS IT CONTAINS
C      SCALING INFO WE DON'T NEED ANY MORE
      READ(2,2000)A
2000  FORMAT(1A1)
C      RESET FAILURE FLAG
      IFAIL=0
C      DRAW FIRST DATA SET WITH AXES
      CALL UNLOAD(1)
C      IF FAILURE FLAG SET, FORGET IT
      IF(IFAIL.NE.0)GOTO 20
C      NO, SO PLOT IT
      CALL PLOTIT(0)
C      CANCEL ORIGIN SHIFT LEFT FROM PLOTIT
      CALL PLOT(-(XLEN+5.0),0.0,-3)
C      SET UP LOOP TO UNLOAD THE REST OF THE DATA FROM THE
C      SCRATCH FILE, ONE SET AT A TIME
      DO 100 I=2,10
      CALL UNLOAD(I)
C      IF FAILURE FLAG SET, ABORT LOOP
      IF(IFAIL.NE.0)GOTO 20
C      NO, SO PLOT IT
      CALL LINE(XDAT,YDAT,NPTS,1,0,0)
C      REPOSITION PEN TO START NEXT LINE
      CALL PLOT(0.0,0.0,3)
100   CONTINUE
20    CONTINUE
C      CLOSE GRAPHICS
      CALL PLOT(30.0,0.0,999)
      STOP
      END
C
C******
C
C
